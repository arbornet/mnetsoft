% Backtalk Wasabi Interface -- (c) 2004, Jan Wolter
%
% response script
%
% A script to post a response to an item.  This is the form handler for the
% response form on the bottom of the screen generated by "read.mktext.bt"
%
% Query Parameters:
%    text=      text of response to post
%    texttype=  type of text ("text/html" or "text/plain")
%    conf=      current conference
%    item=      item to post to
%    alias=     full name to post under.  Defaults if not given.

% Open a conference
open_conf {
  (Error: Could not open conference ')conf('.\n)
  halt
} if

mayread ! {
  (You cannot read this conference.)
  halt
} if

text ! {
  (Your message was empty, so there was nothing to post. Go back )
  (and fill in the response area before pressing "post") stop
} if

% Get mayresp and frozen variables, and check them
texttype
(0) item this_item { (No article number )item halt } if
read_item { (Article )item( does not exist) halt) } if
pop
/texttype xstore

mayresp ! {
  (Because of improper permissions, your message has not been posted.<BR>)
  id ! {
    ((You have not been authenticated.))
  } {
    frozen {`(\(This )Item( is frozen.\))'}
           {`(\(This )Conference( is read-only.\))'} ifelse
  } ifelse
  stop
} if

% Netscape's auto text wrapping mades this unnecessary - but Internet Explorer
% got it wrong, so here it is again
texttype (text/plain) eq
{
   text 79 wrap /text xstore
} if

texttype (text/html-blanks) eq
{
   text (\r) () 0 replace (\n\n) (\n<P>\n) 0 replace /text xstore
   /texttype (text/html) store
} if

/erased 0 def

% Get the user's full name if it wasn't passed in
alias ! {
  id {
    % Get user's full name, if registered
    /alias () /usrname pushuser {(unknown)} if def
  } {
    anonposter /usrname pushuser ! { /alias xstore } if
    alias ! {/alias (Anonymous User) store } if
  } ifelse
} if

% finished checking for errors, now we'll actually try to write the message

(lib/postlog) include

post_resp /resp xstore

resp 0 le {
  (Your post failed due to an internal error.\n) halt
} if

/http_location
  `backtalkurl(/)flavor(begin?conf=)conf(&item=)item(&show=)resp(-$)'
  store

