% Backtalk Abalone Interface -- (c) 2000, Jan Wolter, Steve Weiss
%
% newitem script
%
% A script to enter new items.  This is the form handler for the "enteritem.bt"
% script.  This jumps to the read.bt screen after you enter the item.
%
% Query Paramters:
%   conf=       name of conference to post to
%   csel=       list of conferences to read (not used, just passed along)
%   text=	text of the item to post
%   texttype=   type of content posted (text/plain or text/html)
%   title=	title of the item to post
%   pseudo=	pseudonym to use (if not defined, use user's conference alias)
%   attachments=list of attachments
%
% Only if Javascript is off:
%   atch=       should we get more attachments?
%   baa_file=   last attachemnt
%   desc=       it's discription

(lib/postlog) include

% we accumulate multiple error messages in the "bad" variable
() /bad xdef

% Attachment handling for users with Javascript turned off
canattach { /atch defined { atch (1) eq {
  /item -1 def
  /inmain 1 def
  flavor(askattach)+ jump
} if } if } if

flavor(openconf)+ stopped
 {(.<P>)+ /bad xdef} if

secret 2 eq /dunno defined ! and {
  (lib/biolink) include
  [/conf /csel /text /texttype /title /pseudo] flavor(confpass)+ jump
} if

% More attachment handling for users with Javascript off
% Last attachment submitted needs to be indexed
canattach {/aapost defined { baa_file {
  /item maxitem 1 + def
  baa_file desc 0 make_attach /baa_file xdef
  /attachments `attachments {attachments (,)} if baa_file' def
} if } if } if

/allowhtml dflt_html def (*settings) [/allowhtml /allowgraphics] loadvar pop
allowhtml cvi ! /texttype defined ! or {
  /texttype (text/plain) def
} if

maypost !
 {`bad(Cannot write conference )conf(.<P>)' /bad xdef} if

text !
 {bad(Your message was empty. There is nothing to post.<P>)+ /bad xdef} if

title !
 {bad(Your title was empty. You must have a title.<P>)+ /bad xdef} if

bad {(<HTML><BODY>)bad(</BODY></HTML>) halt} if

pseudo {pseudo /alias xstore} if

texttype (text/plain) eq
{
   % rewrap the text
   text 72 wrap /text xstore
} if

texttype (text/html-blanks) eq
{
   % convert blank lines into <P> directives
   text (\r) () 0 replace (\n\n) (\n<P>\n) 0 replace /text xstore
   /texttype (text/html) store
} if

% do the post
post_item

% go read the whole item
/http_location `backtalkurl(/)flavor(read?conf=)conf(&csel=)csel
 (&item=)item(&rsel=all)
 /nextnew defined{(&nextnew=)nextnew}if '
 store
