% Backtalk Pistachio Interface -- (c) 1996, Jan Wolter, Steve Weiss
%
% read.text script
%
% This is the text window frame used with the read screen when Pistachio is
% in framed mode.  It is invoked by the frame structure generated by "read.bt".
% Most of the work here is done by "read.mktext.bt".
%
% Query Parameters:
%    conf=      current conference
%    csel=      selector for future conferences to read
%    item=      item to read now
%    isel=      selector for items to read after this
%    rsel=      response selector - (all) (new) (since) or numeric range
%    tmprsel=   responses to actually show - (back) (fore) (first) (last) range
%    noskip=    if true, display even items with no selected responses.
%    since=     if rsel is (since) this gives date.
%    showforgotten= show all, unforgotten, or forgotten items.

/frames 1 def /rpage 30 def /rtype (text/plain) def /twitlist () def
(*.backtalk) [/rpage /rtype /timezone /twitlist] loadvar pop

flavor(config)+ include

(<HTML>
<HEAD>
<STYLE TYPE="text/css">
<!--
TEXTAREA {font-family: monospace;}
//-->
</STYLE>
)/metatags(</HEAD>
)

flavor(openconf)+ stopped {Conference cap( )conf( does not exist\n) halt} if
/butsel 73 defconstant flavor(buttondef)+ include

(<BASE TARGET="_top">
)

mayread ! {(The )conf( )Conference( cannot be read.) halt } if

/tmprsel defined
{ tmprsel (back) eq
  { at cvi rpage - dup 0 lt {pop 0} if cvs (-)+ at cvi 1 - cvs + }
{ tmprsel (fore) eq
  { at cvi 1 + cvs (-$) + }
{ tmprsel (first) eq
  { (0-) at cvi dup rpage gt {pop rpage} if 1 - cvs + }
{ tmprsel (last) eq
  { (-) rpage cvs + }
{ tmprsel } ifelse } ifelse } ifelse } ifelse }
{ rsel }
ifelse
item this_item {`(No )Item( )'item( in )conf`( )Conference(.\n)' halt} if
/nrs xstore

flavor(read.mktext)+ include

global_footer
(</BODY></HTML>
)
