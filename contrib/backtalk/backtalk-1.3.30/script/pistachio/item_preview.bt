% Backtalk Pistachio Interface -- (c) 1996, Jan Wolter, Steve Weiss
%
% item preview script
%
% A script to preview an item.  This is a form handler for the "enter.bt"
% script.
%
% Query Parameters:
%    conf=      conference to post in.
%    text=	text of response to post
%    texttype=  type of text ("text/html" or "text/plain")
%    title=     title of the item.
%    pseudo=    name to post response under
%    language=  language to spellcheck in

flavor(config)+ include

canspell {
  /language defined ! {/language () def} if
  language (OFF) eq {
    /spellon 0 def
  } {
    /spell_lang language store
    /spellon 1 def
  } ifelse

  % <text> dospell <text>
  % Find all the mispelled words and highlight them, if spellchecking is on
  /dospell {
    spellon {
      ` exch (text/html) 1
        {(<FONT COLOR=)highcolor(>)4 -1 roll(</FONT>)}
      spellcheck '
    } if
  } def
} if

(<HTML>
<HEAD>
<TITLE>)Item cap( Preview</TITLE>
)/metatags(</HEAD>
)

flavor(openconf)+ stopped {
  (Backtalk internal error: No such )Conference( \()conf(\)\n) halt
} if

global_header

(*settings) /allowgraphics loadvar pop

pseudo {pseudo /alias xstore} if

/now time def

(<H1>Preview of how your )Item( will look to other Backtalk users:</H1>
<HR><P>
<STRONG><FONT SIZE=+1>)bbsname( )conftitle( )Item cap( )maxitem 1 +
(: <I>)title expand canspell {dospell} if(</I><BR>
Entered by )alias quote(</I> \(<A HREF="bio?login=)id(">)
id expand(</A>\) on )now ztime (:</FONT></STRONG><P>\n)

texttype (text/plain) eq {
  % Plain Text
  (<PRE>)text expand canspell {dospell} if(</PRE>)
  /text_interface constant {
     /plain () def
  } if
}{
  texttype (text/html) eq {
    % HTML Text
    text cleanhtml
    /text_interface constant {
      dup unhtml /plain xdef
    } if
    canspell {dospell} if
  }{
    % Lazy-HTML Text
    text (\r) () 0 replace
    (\n\n) (\n<P>\n) 0 replace
    cleanhtml
    /text_interface constant {
      dup unhtml /plain xdef
    } if
    canspell {dospell} if
  } ifelse
  (<BR CLEAR=ALL>\n)
} ifelse

(<HR>\n)
canspell {
  (<FORM ACTION=item_preview>
  <INPUT TYPE=hidden NAME=text VALUE=")text quote(">
  <INPUT TYPE=hidden NAME=texttype VALUE=")texttype quote(">
  <INPUT TYPE=hidden NAME=pseudo VALUE=")pseudo quote(">
  <INPUT TYPE=hidden NAME=title VALUE=")title quote(">
  <INPUT TYPE=hidden NAME=hidden VALUE=")hidden(">
  <INPUT TYPE=hidden NAME=conf VALUE=")conf(">
  Spellchecking: <SELECT NAME=language>\n)
  language ! {(<OPTION VALUE="">Default Language\n)} if
  [languages (,) split] {
    /l xdef
    (<OPTION VALUE=")l(") language l eq {( SELECTED)}if (>)l cap(\n)
  } forall
  (<OPTION VALUE="OFF") language (OFF) eq {( SELECTED)}if (>Off
  </SELECT><INPUT TYPE=submit VALUE="Recheck">)
  language (OFF) ne {(&nbsp;&nbsp;&nbsp;&nbsp;
  Possible misspelled words are <FONT COLOR=)highcolor
  (>highlighted</FONT>.)} if
  (\n)
} if

/text_interface constant {
  plain {
    (<H1>Preview of how your )Item( will look to )text_interface+
    ( users:</H1>\n)+
    (<HR><P><PRE>\n)+
    bbsname( )conftitle( Item )maxitem 1 +(: )title expand(\n Entered by )
    alias quote( \()id quote(\) on ) now ztime(\n\n)
    plain expand
    (</PRE>\n<HR>\n)
  } if
} if

(<P>Use your browser's "Back" button to return to the )Item( entry form
)
global_footer
(</BODY>
</HTML>
)
