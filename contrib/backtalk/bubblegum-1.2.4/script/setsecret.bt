% Backtalk Bubblegum Interface -- (c) 1996, Jan Wolter, Steve Weiss
%
% setsecret script
%
% This script presents a form to reset the conference password for secret
% conferences.  If "newsecret" is defined, this acts as the form handler.
% Otherwise, it generates the form.
%
% Query Parameters:
%    conf=	name of conference where we will change the alias
%    newsecret=	new password to set
%    csel=      pass this on to confhome.bt
%    nextnew=   pass this on to confhome.bt

/newsecret defined ! {/newsecret () def} if

flavor(config)+ include

`(
<HTML><HEAD>
<TITLE>)Conference cap( Password Change Form</TITLE>
<BASE HREF=")backtalkurl(/)flavor(">
</HEAD>)

% Open the conference
flavor(openconf)+ stopped {
  ( Backtalk internal error: No such )Conference( \()conf(\)\n) halt}
if

global_header

% have to give password before you can change it
secret 2 eq { [/conf /csel /nextnew /newsecret] flavor(confpass)+ jump } if

% can't change a password in a conference that hasn't got one.
secret 0 eq {
  (This )Conference( is not password protected.\n</BODY></HTML>\n)
  halt
} if

% only fairwitnesses can change passwords
amfw ! {
  `(You are not a )Fairwitness( in this )Conference(.\n</BODY></HTML>\n)'
  halt
} if

% Don't allow "Dunno!" as a password.
newsecret (Dunno!) eq {
  (You cannot set a )Conference('s password to "Dunno!" - this is reserved for
  internal use.<P>\n)
} {
  % new password given - change it
  newsecret {
    clear

    % write out the password
    newsecret(\n)+ (*secret) write

    %call the confhome page
    flavor(confhome)+ jump
  } if
} ifelse

% Otherwise, ask for a password
(Please enter the new password for the )conf( )Conference(:
<FORM METHOD=POST ACTION=setsecret>
<INPUT TYPE=hidden NAME=conf VALUE=")conf(">\n)
csel { (<INPUT TYPE=hidden NAME=csel VALUE=")csel(">\n) } if
/nextnew defined { (<INPUT TYPE=hidden NAME=nextnew VALUE=")nextnew(">\n) } if
(<INPUT NAME=newsecret TYPE=password SIZE=20>
<INPUT TYPE=submit VALUE="Change Password">
</FORM>
</BODY>
</HTML>
)
