% Backtalk Bubblegum Interface -- (c) 1996, Jan Wolter, Steve Weiss
%
% respedit script
%
% A script to post a response to an item.  This is the form handler for the
% response form on the bottom of the screen generated by "read.mktext.bt"
%
% Query Parameters:
%    text=	text of response to post
%    texttype=  type of text ("text/html" or "text/plain")
%    pseudo=     name to post response under
%    post.x=    defined if the "post" button was clicked.
%    preview.x= defined if the "preview" button was clicked.
%    lastresp=  number of last response
%    newtitle=  if given, new title for item
%
%    conf=      current conference
%    csel=      selector for future conferences to read
%    item=      item to read now
%    isel=      selector for items to read after this
%    rsel=      response selector - (all) (new) (since) or numeric range
%    noskip=    if true, display even items with no selected responses.
%    since=     if rsel is (since) this gives date.
%    showforgotten= show all, unforgotten, or forgotten items.

flavor(config)+ include

% if previewing run another script
/preview.x defined {
  /thisresp resp def
  flavor(resp_preview)+ jump
} if

flavor(openconf)+ stopped {( )Conference cap( )conf( does not exist.\n) halt} if

mayread ! {(The )conf( )Conference( is not readable to you.) stop } if

pseudo {pseudo /alias xstore} if

/allowhtml dflt_html def (*settings) [/allowhtml /allowgraphics] loadvar pop
allowhtml ! /texttype defined ! or {
   /texttype (text/plain) def
} if

%Netscape's auto text wrapping mades this unnecessary - but Internet Explorer
%got it wrong, so here it is again
texttype (text/plain) eq
{
   text 79 wrap /text xstore
} if

texttype (text/html-blanks) eq
{
   text (\r) () 0 replace (\n\n) (\n<P>\n) 0 replace /text xstore
   /texttype (text/html) store
} if

edit_resp /err xdef

err 0 gt {
  err 2 eq {
    `(No such )Response( or )Item( is frozen.\n)'
  } {
    `(Permission to edit )Response( denied.\n)'
  } ifelse
  stop
} if

/newtitle defined {
  newtitle retitle_item {
    `(Permission to edit )Response( denied.\n)'
    stop
  } if
} if

% show my response and successors
resp cvs (-$) + /tmprsel xdef
flavor(read)+ jump
